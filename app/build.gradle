plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'

    // Google services Gradle plugin
    id 'com.google.gms.google-services'

    id 'kotlin-android'

    // Crashlytics Gradle plugin
    id 'com.google.firebase.crashlytics'

    // Performance Monitoring Gradle plugin
    id 'com.google.firebase.firebase-perf'

    // AboutLibraries
    id 'com.mikepenz.aboutlibraries.plugin'

    // Dependency Injection with Hilt
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
}

android {
    namespace 'com.mohandass.botforge'
    compileSdk 33

    defaultConfig {
        applicationId "com.mohandass.botforge"
        minSdk 28
        targetSdk 33
        versionCode 19
        versionName "1.1.9"

        vectorDrawables {
            useSupportLibrary true
        }
        signingConfig signingConfigs.debug

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.2.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    // Compose BOM
    implementation platform("androidx.compose:compose-bom:$compose_bom_version")
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material'
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"

    implementation "androidx.activity:activity-compose:$activity_version"

    implementation "androidx.core:core-ktx:$core_version"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycleVersion"

    // Navigation
    implementation "androidx.navigation:navigation-compose:$nav_version"

    // LiveData
    implementation("androidx.compose.runtime:runtime-livedata:$compose_runtime_version")

    // DataStore Preferences
    implementation "androidx.datastore:datastore-preferences:$datastore_preferences_version"

    // Room
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    // Kotlin annotation processing tool (kapt)
    kapt "androidx.room:room-compiler:$room_version"

    // Dependency Injection with Hilt
    implementation "com.google.dagger:hilt-android:$dagger_hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_hilt_version"

    implementation "androidx.hilt:hilt-navigation-compose:$hilt_nav_version"

    // Firebase BoM
    implementation platform("com.google.firebase:firebase-bom:$firebase_bom_version")
    // Firebase SDK for Google Analytics, Crashlytics, Authentication, DB, AppCheck
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation "com.google.firebase:firebase-auth-ktx"
    implementation 'com.google.firebase:firebase-database-ktx'
    implementation "com.google.firebase:firebase-firestore-ktx"
    implementation 'com.google.firebase:firebase-appcheck-playintegrity'
    implementation 'com.google.firebase:firebase-perf-ktx'
    implementation 'com.google.firebase:firebase-appcheck-ktx'

    implementation 'com.google.android.gms:play-services-auth:20.4.1'

    // gson
    implementation "com.google.code.gson:gson:$gson_version"

    // OpenAI Kotlin
    implementation platform("com.aallam.openai:openai-client-bom:$openai_kotlin_version")
    implementation 'com.aallam.openai:openai-client'
    implementation 'io.ktor:ktor-client-okhttp'

    // Scale units as DH (Device Height) and DW (Device Width)
    implementation "com.github.slaviboy:JetpackComposePercentageUnits:$percentage_units_version"

    // pretty time
    implementation "org.ocpsoft.prettytime:prettytime:$prettytime_version"

    // AboutLibraries
    implementation "com.mikepenz:aboutlibraries-core:${AboutLibsRelease}"
    implementation "com.mikepenz:aboutlibraries-compose:${AboutLibsRelease}"

    // Accompanist Navigation Animation
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"

    // Compose Markdown
    implementation "com.github.jeziellago:compose-markdown:$compose_markdown_version"
    implementation("io.coil-kt:coil-compose:$coil_version")
}

// Dependency Injection with Hilt
// Allow references to generated code
kapt {
    correctErrorTypes true
}
